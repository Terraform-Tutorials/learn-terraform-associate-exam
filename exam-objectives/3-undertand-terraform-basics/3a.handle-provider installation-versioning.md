# Handle Terraform and provider installation and versioning

##  Provider Configuration
O Terraform usa providers para suportar a interação com os provedores de cloud como AWS, Azure e outros, SaaS providers e APIs. As configurações do provider pertecem ao módulo root da configuração do Terraform.

```hcl
provider "google" {
  project = "acme-app"
  region  = "us-central1"
}
```

## Arguments for Specifying provider

### Version Number Arguments
- >=1.0 - maior do que ou igual a versao
- <=1.0 - menor do que ou igual a versao
- ~>2.0 - qualquer versao acima de 2.0
- >=2.10,<=2.30 - qualquer versao entre 2.10 e 2.30

- O nome `google` dentro do provider mostra que é um (local name) desse provedor.
- o corpo de texto dentro das chaves, indicam que são argumentos da coniguração para esse provider, os dois argumentos project e region são argumentos do provedor google.
- O Terraform Registry é o local onde estão todos os provedores utilizados pelo Terraform, com todas as documentações e dados de cada provedor.
- Existem apenas dois meta argumentos que são nativos do bloco do Terraform que são: `alias` e `version`.

## Multiple Provider Configurations
Também é possível de definir multiplas configurações dentro do mesmo provider, podemos adiconar por exemplo várias regiões de um provedor de nuvem como AWS, ou vários hosts dop Docker ou vários hosts do Consul por exemplo.

Veja um exemplo abaixo de múltiplos provider com o Terraform. Vale detalhes que  devemos sempre usar `alias`.

```hcl
# The default provider configuration; resources that begin with `aws_` will use
# it as the default, and it can be referenced as `aws`.
provider "aws" {
  region = "us-east-1"
}

# Additional provider configuration for west coast region; resources can
# reference this as `aws.west`.
provider "aws" {
  alias  = "west"
  region = "us-west-2"
}
```

Para modulos do Terraform, devemos seguir com a documentação do Terraform, que pede para em casos de uso com módulos e vários provedores dentro do Terraform, devemos seguir com o argumento `configuration_aliases` dentro de `required_providers` como esta abaixo:

```hcl
terraform {
  required_providers {
    mycloud = {
      source  = "mycorp/mycloud"
      version = "~> 1.0"
      configuration_aliases = [ mycloud.alternate ]
    }
  }
}
```
## Selecting Alternate Provider Configurations
Por default, os recursos do Terraform usam as configuracoes padroes de cada provide (sem o `alias`), caso voce esteja querendo usar um provider alternativo, deve-se passar o argumento dentro do campo do `resource` como abaixo:

```hcl
resource "aws_instance" "foo" {
  provider = aws.west

  # ...
}
```

Para uso dentro de modulos usamos da seguinte forma:

```hcl
module "aws_vpc" {
  source = "./aws_vpc"
  providers = {
    aws = aws.west
  }
}
```

# Terraform Settings
O Terraform prmite que seja passado um bloco especial antes de iniciar os ajustes dos providers, esse bloco tem o objetivo de definir algumas configurações do próprio Terraform, como `version`, e outros dados.

## Terraform Block Syntax
Para que possamos seguir com o bloco do Terraform, siga o exemplo abaixo:

```hcl
terraform {
  # ...
}
```

## Specifying a Required Terraform Version
Tambem podemos usar o bloco de `required_version` dentro do Terraform. Esse tipo de bloco aceita somente `version contraints string` que especifica o modelo de versao que esta sendo adotado.

Se voce estiver usando uma versao que esta fora da especificada dentro da `version contraints string` o Terraform vai produzir um erro informando que esta com inconsistencia de versoes.

- As `version contraints string` são ideias para ambientes colaborativos, garantindo que todos do time possam usufruir da mesma versão.
- O modelo da `version contraints string` se parece com o seguinte `version = ">= 1.2.0, < 2.0.0"`
- Aceita os seguintes operadores:
  
    `=` (or no operator): Allows only one exact version number. Cannot be combined with other conditions.

    `!=`: Excludes an exact version number.

    `>, >=, <, <=`: Comparisons against a specified version, allowing versions for which the comparison is true. "Greater-than" requests newer versions, and "less-than" requests older versions.

    `~>`: Allows only the rightmost version component to increment. For example, to allow new patch releases within a specific minor release, use the full version number: ~> 1.0.4 will allow installation of 1.0.5 and 1.0.10 but not 1.1.0. This is usually called the pessimistic constraint operator.

## Specifying Provider Requirements
Existe tambem o bloco de `required_providers` que especifica detalhes de cada provider que esta sendo usado.

```hcl
terraform {
  required_providers {
    aws = {
      version = ">= 2.7.0"
      source = "hashicorp/aws"
    }
  }
}
```

## Experimental Language Features
Podemos usar tambem a feature chamada de `experiments` que testa se determinada feature ou outra funcionalidade esta sendo bem implementada. Isso deve ser passado dentro do bloco do `terraform`.

```hcl
terraform {
  experiments = [example]
}
```

# Install Terraform
Vamos ver nessa sessao os conceitos de instalar o Terraform nos sistemas operacionais que estao sendo suportados pela ferramenta segundo a Hashicorp.

## Manual Installation
Para que isso seja possivel devemos seguir com os passos abaixo:

- Encontre primeiro o pacote apropriado que atende melhor dentro do seu contexto e faca download dele (diretamente do site do Terraform).
- Depois e ter feito download, use o comando `unzip` para descompactar os dados.
- Existe um binario escrito em `Go` que se encarrega de tudo para que o Terraform funcione.
- Finalmente, garanta que esse binario esta dentro do seu `PATH`.

`$ echo $PATH`

`$ mv ~/Downloads/terraform /usr/local/bin/`

`$ terraform -help`

# Lock and Upgrade Provider Versions
Alguns recursos que são bem úteis dentro do Terraform e que ajudam demais. Quando estamos com várias pessoas usando as configurações do Terraform, temos que usar a mesma versão do providers e existem duas formas para gerenciar isso dentro do Terraform:

- Usar o  bloco de `terraform` e ajustar a constraint dentro do bloco com a versao.
- Outra forma seria usar a dependencia do `lock file` no Terraform.

Se a gente não especifica a versão, o Terraform vai assumir o default que é baixar a última versão mais recente disponível na documentação da ferramenta.

```hcl
terraform {
  required_providers {
    random = {
      source  = "hashicorp/random"
      version = "3.1.0"
    }

    aws = {
      source  = "hashicorp/aws"
      version = ">= 2.0.0"
    }
  }

  required_version = ">= 1.0.0"
}
```

Observe que para esse exemplo acima, estamos usando o bloco do `terraform`, e adicionamos algumas opcoes:

- o nome do provider que estamos usando que e AWS.
- o `source address` que estamos passando.
- e a versao que estamos usando para esse provider.

